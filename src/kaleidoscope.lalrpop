use std::str::FromStr;
use crate::ast::*;

grammar;

// macro for precedence climbing for binary operations
Tier<Op,NextTier>: Box<Exp> = {
    <l:Tier<Op,NextTier>> <bop:Op> <r:NextTier> => Box::new(Exp::BinOp{
        op: bop,
        lhs: l,
        rhs: r,
    }),
    NextTier
};

// comma-separated list macro
List<T>: Vec<T> = {
    <ds:(<T> ",")*> <d:T> => {
        let mut ds = ds;
        ds.push(d);
        ds
    }
}


// rule for the entire program
// used to generate the actual parser in main.rs
pub Prog: Prog = {
    <e:Extern*> <f:(Func+)> => Prog{
        externs: e, 
        funcs: f,
    }
};

Extern: Extern = {
    "extern" <t:Type> <gid:GlobId> "(" <tds:TDecls?> ")" ";" => Extern{
        type_: t,
        globid: gid,
        args    : tds,
    }
};

Func: Func = {
    "def" <t:Type> <gid:GlobId> "(" <vs:(VDecls)?> ")" <b:Blk> => Func{
        type_: t,
        globid: gid,
        args: vs,
        blk: b,
    }
};

Blk: Block = {
    "{" <s:(Stmts)?> "}" => Block{ stmts: s },
};

Stmts: Vec<Box<Stmt>> = {
    <s:(Stmt)+> => s,
};

Stmt = StmtRestr<"I">;

StmtRestr<I>: Box<Stmt> = {
    <b:Blk> => Box::new(Stmt::Blk(b)),
    "return" <e:(Exp)?> ";" => Box::new(Stmt::ReturnStmt(e.map(|x| *x))),
    <v:VDecl> "=" <e:Exp> ";" => Box::new(Stmt::VDeclStmt{
        vdecl: v,
        exp: *e,
    }),
    <e:Exp> ";" => Box::new(Stmt::ExpStmt(*e)),
    "while" "(" <c:Exp> ")" <s:StmtRestr<I>> => Box::new(Stmt::WhileStmt{
        cond: *c,
        stmt: s,
    }),
    "print" <e:Exp> ";" => Box::new(Stmt::PrintStmt(*e)),
    "print" <s:Slit> ";" => Box::new(Stmt::PrintStmtSlit(s)),
    IfStmt<I>,
};

IfStmt<I>: Box<Stmt> = {
    "if" "(" <c:Exp> ")" <s:StmtRestr<"I">> if I != "" => Box::new(Stmt::IfStmt{
        cond: *c,
        stmt: s,
        else_stmt: None,
    }),
    "if" "(" <c:Exp> ")" <s:StmtRestr<"">> <e:("else" <StmtRestr<I>>)> => Box::new(Stmt::IfStmt{
        cond: *c,
        stmt: s,
        else_stmt: Some(e),
    }),
};

Exps = List<Exp>;

// lowest-precedence expressions
// see Term for highest prec., -Op rules for other precedences
Exp: Box<Exp> = {
    Assignment,
    BinOp,
};

Assignment: Box<Exp> = {
    <v:VarId> "=" <e:Exp> => Box::new(Exp::Assign{
        varid: v,
        exp: e,
    }),
};

// all binary operations excluding casts and assignments
BinOp: Box<Exp> = {
    // logic operations are lower-precedence than arithmetic, so check those first
    LogicOps
};

LogicOpsOp: BOp = {
    "||" => BOp::Or    
};
LogicOps = Tier<LogicOpsOp, LogicAnd>;

AndOp: BOp = {
    "&&" => BOp::And
};
LogicAnd = Tier<AndOp, LogicEq>;

EqOp: BOp = {
    "==" => BOp::EqTo,
};
LogicEq = Tier<EqOp, LogicComp>;

CompOp: BOp = {
    "<" => BOp::Lt,
    ">" => BOp::Gt,
};
LogicComp = Tier<CompOp, ArithOps>; // we're ready to do arithmetic

ArithOpsOp: BOp = {  // how do you name things
    "+" => BOp::Add,
    "-" => BOp::Sub,
};
ArithOps = Tier<ArithOpsOp, ArithFactor>;

FactorOp: BOp = {
    "*" => BOp::Mult,
    "/" => BOp::Div
};

ArithFactor = Tier<FactorOp, TypeCast>;

// Expressions that need to be parsed first (including high-precedence ops, literals, etc.)
Term: Box<Exp> = {
    "(" <Exp> ")",
    Literal,
    <v:VarId> => Box::new(Exp::VarVal(v)),
    FuncCall,
};

// Needed to put these in the precedence chain somewhere, arbitrarily decided
TypeCast: Box<Exp> = {
    "[" <t:Type> "]" <e:TypeCast> => Box::new(Exp::Cast{
        type_: t,
        exp: e,
    }),
    UOp,
};

UOp: Box<Exp> = {
    "!" <e:UOp> => Box::new(Exp::UnaryOp{
        op: UOp::BitwiseNeg,
        exp: e,
    }),
    "-" <e:UOp> => Box::new(Exp::UnaryOp{
        op: UOp::SignedNeg,
        exp: e,
    }),
    Term,
};

Literal: Box<Exp> = {
    "true"  => Box::new(Exp::Literal(Lit::LitBool(true))),
    "false" => Box::new(Exp::Literal(Lit::LitBool(false))),
    <i:r"[0-9]+"> <f:r"\.[0-9]+"?> => match f {
        None => {
            Box::new(Exp::Literal(Lit::LitInt(i32::from_str(i).unwrap())))
        },
        Some(f) => Box::new(Exp::Literal(Lit::LitFloat(format!("{}{}", i, f).parse().unwrap()))),
    }
};


FuncCall: Box<Exp> = {
    <g:GlobId> "(" <e:Exps?> ")" => Box::new(Exp::FuncCall{
        globid: g,
        exps: e,
    }),
};

Slit: String = {
    r#""[^"\n\r]*""# => String::from(<>),
};

Ident: String = {
    r"[a-zA-Z_]+[a-zA-Z0-9_]*" => String::from(<>),
};

VarId: String = {
    "$" <i:Ident> => i,
};

GlobId: String = {
    Ident
};

Type: Type = {
    "int"   => Type::IntType,
    "cint"  => Type::CIntType,
    "float" => Type::FloatType,
    "bool"  => Type::BoolType,
    "void"  => Type::VoidType,
    <na:"noalias"?> "ref" <t:Type> => match na {
        None    => Type::Ref(false, Box::new(t)),
        Some(_) => Type::Ref(true, Box::new(t)),
    }
};

// TDecls = List<Type>;
TDecls: Vec<Type> = {
    <ds:(<Type> ",")*> <d:Type> => {
        let mut ds = ds;
        ds.push(d);
        ds
    }
}

VDecls = List<VDecl>;

VDecl: VDecl = {
    <t:Type> <i:VarId> => VDecl{
        type_: t,
        varid: i,
    }
    }

// uh I think this ignores comments
match {
    r"#[^\n\r]*[\n\r]*" => { },
} else {
    r"\s*" => { },
    _
}
